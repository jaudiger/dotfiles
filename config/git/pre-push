#!/usr/bin/env nu
#
# Copyright (c) Jérémy Audiger.
# All rights reserved.
#

def validate_branch_name [
    remote_exists: bool
    fail_on_error: bool
]: nothing -> nothing {
    let branch_name = (^git branch --show-current | str trim)
    const branch_regex = '^(bugfix|hotfix|imprv|feat|feature)-i[0-9]+-[a-zA-Z0-9_-]+$'

    # Check if branch matches the required pattern
    if (($branch_name | parse --regex $branch_regex) | is-not-empty) {
        # Exit earlier since the regex matches with the branch name
        return
    }

    # Check if branch exists on remote
    if $remote_exists and ((^git ls-remote --heads origin $branch_name | lines | length) != 0) {
        return
    }

    # Check if it's a special branch
    if ($branch_name in ["main", "master", "develop"]) {
        return
    }

    # If we get here, the branch name is invalid
    const error_message = "Aborting commit push. Branch name format has to be: '<type>(-i<issue>)?-<name>'

'type' must be one of the following:
- bugfix: Fix a bug
- hotfix: Fix a critical bug
- imprv: Overall improvement
- feat|feature: New feature"

    if ($fail_on_error) {
        print -e $error_message
        exit 1
    }

    print $error_message
}

def validate_branch []: nothing -> nothing {
    # Check if the remote origin exists
    if (^git remote | lines | where $it == "origin" | is-empty) {
        validate_branch_name false false
        return
    }

    let remote = (^git remote get-url origin)
    const remote_regex = 'gitlab\.com.IoTerop'

    if (($remote | parse --regex $remote_regex) | is-not-empty) {
        # If the remote is affiliated with work, exit on error if the validation failed
        validate_branch_name true true
    } else {
        validate_branch_name true false
    }
}

# Validate the branch name
def main [
    ...args: any # Additional arguments
]: nothing -> nothing {
    validate_branch
}
